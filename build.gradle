// docker-junit-rule

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.17.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '1.2.15'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.0.13"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.11.0"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "2.0.2"

    // useful utilities
    id "com.arakelian.multitool" version "1.2.0"
}

group   = 'com.arakelian'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

multitool {
    relocates += [
        'com.fasterxml'               : 'repackaged.com.arakelian.docker.junit.${prefix}com.fasterxml',
        'com.github.rholder.retry'    : 'repackaged.com.arakelian.docker.junit.${prefix}com.github.rholder.retry',
        'com.google.common'           : 'repackaged.com.arakelian.docker.junit.${prefix}com.google.common',
        'com.google.thirdparty'       : 'repackaged.com.arakelian.docker.junit.${prefix}com.google.thirdparty',
        'com.kenai'                   : 'repackaged.com.arakelian.docker.junit.${prefix}com.kenai',
        'com.spotify.docker'          : 'repackaged.com.arakelian.docker.junit.${prefix}com.spotify.docker',
        'javax.annotation'            : 'repackaged.com.arakelian.docker.junit.${prefix}javax.annotation',
        'javax.inject'                : 'repackaged.com.arakelian.docker.junit.${prefix}javax.inject',
        'javax.ws.rs'                 : 'repackaged.com.arakelian.docker.junit.${prefix}javax.ws.rs',
        'jersey.repackaged'           : 'repackaged.com.arakelian.docker.junit.${prefix}jersey',
        'jni'                         : 'repackaged.com.arakelian.docker.junit.${prefix}jni',
        'jnr'                         : 'repackaged.com.arakelian.docker.junit.${prefix}jnr',
        'org.apache'                  : 'repackaged.com.arakelian.docker.junit.${prefix}org.apache',
        'org.bouncycastle'            : 'repackaged.com.arakelian.docker.junit.${prefix}org.bouncycastle',
        'org.glassfish'               : 'repackaged.com.arakelian.docker.junit.${prefix}org.glassfish',
        'org.jvnet'                   : 'repackaged.com.arakelian.docker.junit.${prefix}org.jvnet',
    ]
}

dependencies {
    processor 'org.immutables:value:2.5.6'

    // we shadow Guava due to frequent downstream compatibility issues
    shadow 'com.google.guava:guava:23.6-jre'

    // we expose a JUnit rule so obviously JUnit is a runtime requirement
    compile 'junit:junit:4.12'

    // easy-to-use library for communicating with Docker host
    shadow ('com.spotify:docker-client:8.9.2') {
        exclude group: 'org.slf4j'
    }

    // logging
    testCompile 'org.apache.logging.log4j:log4j-api:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.25'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile     'org.slf4j:slf4j-api:1.7.25'

    // for unit testing
    testCompile 'com.github.rholder:guava-retrying:2.0.0'
    testCompile 'com.rabbitmq:amqp-client:4.1.0'
}
