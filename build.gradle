// docker-junit-rule

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.19.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '1.2.15'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.0.14"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.11.0"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "2.0.4"

    // useful utilities
    id "com.arakelian.multitool" version "1.4.2"
}

group   = 'com.arakelian'

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}

multitool {
    relocates += [
        'com.google.common'           : 'repackaged.com.arakelian.docker.junit.${prefix}com.google.common',
        'com.google.errorprone'       : 'repackaged.com.arakelian.docker.junit.${prefix}com.google.errorprone',
        'com.google.j2objc'           : 'repackaged.com.arakelian.docker.junit.${prefix}com.google.j2objc',
        'com.google.thirdparty'       : 'repackaged.com.arakelian.docker.junit.${prefix}com.google.thirdparty',
        'javax.annotation'            : 'repackaged.com.arakelian.docker.junit.${prefix}javax.annotation',
        'org.apache.commons'          : 'repackaged.com.arakelian.docker.junit.${prefix}org.apache.commons',
    ]

    configureMinify = true

    proguardConfiguration {
        keep includedescriptorclasses:true, 'class com.arakelian.** { *; }'
        keep includedescriptorclasses:true, 'class com.kenai.** { *; }'
        keep includedescriptorclasses:true, 'class com.spotify.docker.client.* { *; }'
        keep includedescriptorclasses:true, 'class com.spotify.docker.client.messages.* { *; }'
        keep includedescriptorclasses:true, 'class com.spotify.docker.client.shaded.** { *; }'
        keep includedescriptorclasses:true, 'class jersey.repackaged.** { *; }'
        keep includedescriptorclasses:true, 'class jni.** { *; }'
        keep includedescriptorclasses:true, 'class jnr.** { *; }'
        keep includedescriptorclasses:true, 'class mozilla.** { *; }'
        keep includedescriptorclasses:true, 'class org.aopalliance.** { *; }'
        keep includedescriptorclasses:true, 'class org.jvnet.** { *; }'

        // dont change original class files
        dontoptimize()

        // no warnings
        dontnote()

        optimizationpasses 1
    }
}

dependencies {
    processor 'org.immutables:value:2.6.1'

    // we expose a JUnit rule so obviously JUnit is a runtime requirement
    compile 'junit:junit:4.12'

    // we shadow Guava due to frequent downstream compatibility issues
    shadow 'com.google.guava:guava:25.1-jre'

    // shadow because we use very few classes from here and don't want to create
    // unnecessary downstream dependencies
    shadow  'org.apache.commons:commons-lang3:3.7'

    // used dynamically by shaded docker-client http
    shadow ('org.slf4j:jcl-over-slf4j:1.7.25') {
        transitive = false
    }

    // easy-to-use library for communicating with Docker host; we shadow the library
    // to avoid downstream compatibility issues
    shadow ('com.spotify:docker-client:8.11.6:shaded') {
        transitive = false
    }

    // logging
    testCompile 'org.apache.logging.log4j:log4j-api:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.25'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile     'org.slf4j:slf4j-api:1.7.25'

    // for unit testing
    testCompile 'com.github.rholder:guava-retrying:2.0.0'
    testCompile 'com.rabbitmq:amqp-client:4.1.0'
}
